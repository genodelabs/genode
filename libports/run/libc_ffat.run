#
# \brief  Test for using the libc_ffat plugin
# \author Christian Prochaska
# \date   2011-05-27
#

if {[catch { exec which mkfs.vfat } ]} {
	puts stderr "Error: mkfs.vfat not installed, aborting test"; exit }

if {[have_spec linux]} {
	puts "Run script does not support this platform"; exit }

#
# Build
#

build {
	core init
	drivers/pci
	drivers/atapi
	drivers/timer
	drivers/sd_card
	test/libc_ffat
}

create_boot_directory

#
# Generate config
#

set config {
<config verbose="yes">
	<parent-provides>
		<service name="ROM"/>
		<service name="RAM"/>
		<service name="IRQ"/>
		<service name="IO_MEM"/>
		<service name="IO_PORT"/>
		<service name="CAP"/>
		<service name="PD"/>
		<service name="RM"/>
		<service name="CPU"/>
		<service name="LOG"/>
		<service name="SIGNAL"/>
	</parent-provides>
	<default-route>
		<any-service> <parent/> <any-child/> </any-service>
	</default-route>
	<start name="timer">
		<resource name="RAM" quantum="1M"/>
		<provides> <service name="Timer"/> </provides>
	</start>
	<start name="test-libc_ffat">
		<resource name="RAM" quantum="2M"/>
		<config>
			<iterations value="1"/>
		</config>
	</start>
}

append_if [have_spec pci] config {
	<start name="pci_drv">
		<resource name="RAM" quantum="2M"/>
		<provides> <service name="PCI"/> </provides>
	</start>
	<start name="atapi_drv">
		<resource name="RAM" quantum="1M"/>
		<provides> <service name="Block"/> </provides>
		<config ata="yes" />
	</start>
}

append_if [have_spec pl180] config {
	<start name="sd_card_drv">
		<resource name="RAM" quantum="1M" />
		<provides><service name="Block"/></provides>
	</start>
}

append config {
</config>
}

install_config $config

#
# Boot modules
#

# generic modules
set boot_modules {
	core init timer
	ld.lib.so libc.lib.so libc_log.lib.so libc_ffat.lib.so
	test-libc_ffat
}

append_if [have_spec pci]   boot_modules { pci_drv atapi_drv }
append_if [have_spec pl180] boot_modules { sd_card_drv }

build_boot_image $boot_modules

#
# Execute test case
#

set disk_image "bin/test.hda"
set cmd "dd if=/dev/zero of=$disk_image bs=1024 count=65536"
puts "creating disk image: $cmd"
catch { exec sh -c $cmd }

set cmd "mkfs.vfat -F32 $disk_image"
puts "formating disk image with vfat file system: $cmd"
catch { exec sh -c $cmd }

#
# Qemu
#
append qemu_args " -m 128 -nographic "
append_if [have_spec   pci] qemu_args " -hda $disk_image -boot order=d "
append_if [have_spec pl180] qemu_args " -drive file=$disk_image,if=sd,cache=writeback "

run_genode_until {.*child exited with exit value 0.*} 60

exec rm -f $disk_image

puts "\ntest succeeded\n"

# vi: set ft=tcl :
