proc binary_name_ld_lib_so { } { return "ld-pistachio.lib.so" }
proc binary_name_core_o    { } { return "core-pistachio.o"    }
proc binary_name_timer     { } { return "pit_timer_drv" }

proc kernel_files { } { return { pistachio sigma0-pistachio kickstart-pistachio } }

proc run_boot_string   { } { return "\n\r\033\\\[1m\033\\\[33mL4Ka::Pistachio -" }
proc core_link_address { } { return "0x02000000" }


##
# Populdate boot directory with binaries on pistachio
#
proc run_boot_dir {binaries} {

	#
	# Build kernel and dynamic linker on demand, if not yet present in
	# '[run_dir]/genode/'
	#
	set kernel_arg ""
	set ld_arg     ""
	if {![file exists [run_dir]/genode/pistachio]} { set kernel_arg "kernel/pistachio" }
	if {![file exists [run_dir]/genode/ld.lib.so]} { set ld_arg     "lib/ld/pistachio" }
	set targets "$kernel_arg $ld_arg"

	if {[llength $targets]} { build $targets }

	if {$kernel_arg != ""} {
		file copy -force bin/pistachio           [run_dir]/genode/pistachio
		file copy -force bin/sigma0-pistachio    [run_dir]/genode/sigma0-pistachio
		file copy -force bin/kickstart-pistachio [run_dir]/genode/kickstart-pistachio
	}

	build_core_image $binaries

	#
	# Move kernel files to distinct location within the boot directory so that
	# we can remove [run_dir]/genode after the core image has been built.
	#
	exec mkdir -p [run_dir]/pistachio
	file copy -force [run_dir]/genode/pistachio           [run_dir]/kernel
	file copy -force [run_dir]/genode/sigma0-pistachio    [run_dir]/sigma0
	file copy -force [run_dir]/genode/kickstart-pistachio [run_dir]/kickstart
	exec rm -r [run_dir]/genode

	if {[have_include "image/iso"] || [have_include "image/disk"]} {

		if {[have_include "image/disk"]} {
			exec mkdir -p [run_dir]/boot/grub
			exec cp [genode_dir]/tool/boot/bender [run_dir]/boot/bender
		}

		if {[have_include "image/iso"]} {
			install_iso_bootloader_to_run_dir
		}

		#
		# Generate GRUB config file
		#
		# The core binary is part of the 'binaries' list but it must
		# appear right after 'sigma0' as boot module. Hence the special case.
		#
		set fh [open "[run_dir]/boot/grub/grub.cfg" "WRONLY CREAT TRUNC"]
		puts $fh "set timeout=0"
		puts $fh "menuentry 'Genode on L4ka::Pistachio' {"
		puts $fh " insmod multiboot"
		puts $fh " multiboot /boot/bender"
		puts $fh " module /kickstart"
		puts $fh " module /kernel"
		puts $fh " module /sigma0"
		puts $fh " module /image.elf"
		puts $fh "}"
		close $fh
	}

	#
	# Build image
	#
	run_image

	if {[have_include "load/tftp"]} {
		#
		# Install PXE bootloader pulsar
		#
		install_pxe_bootloader_to_run_dir

		#
		# Generate pulsar config file
		#
		set fh [open "[run_dir]/config-52-54-00-12-34-56" "WRONLY CREAT TRUNC"]
		puts $fh " exec /boot/bender"
		puts $fh " load /kickstart"
		puts $fh " load /kernel"
		puts $fh " load /sigma0"
		puts $fh " load /image.elf"
		close $fh

		generate_tftp_config
	}
}


##
# Base source archive within depot
#
proc base_src { } { return base-pistachio }

