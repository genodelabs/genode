proc binary_name_ld_lib_so { } { return "ld-pistachio.lib.so" }
proc binary_name_core_o    { } { return "pistachio/core.o"    }
proc binary_name_timer     { } { return "pistachio_timer_drv" }


proc run_boot_string   { } { return "\n\r\033\\\[1m\033\\\[33mL4Ka::Pistachio -" }
proc core_link_address { } { return "0x02000000" }


##
# Install files needed to boot via PXE
#
proc install_pxe_bootloader_to_run_dir { } {
	exec cp [genode_dir]/tool/boot/pulsar [run_dir]/boot/pulsar
	exec cp [genode_dir]/tool/boot/bender [run_dir]/boot/bender
}


##
# Populdate boot directory with binaries on pistachio
#
proc run_boot_dir {binaries} {

	build { lib/ld/pistachio kernel/pistachio }

	build_core_image $binaries

	exec mkdir -p [run_dir]/pistachio

	exec cp kernel/pistachio/kernel    [run_dir]/pistachio
	exec cp kernel/pistachio/sigma0    [run_dir]/pistachio
	exec cp kernel/pistachio/kickstart [run_dir]/pistachio

	if {[have_include "image/iso"] || [have_include "image/disk"]} {

		#
		# Install isolinux/GRUB files and bender
		#
		install_iso_bootloader_to_run_dir

		#
		# Generate grub config file
		#
		# The core binary is part of the 'binaries' list but it must
		# appear right after 'sigma0' as boot module. Hence the special case.
		#
		set fh [open "[run_dir]/boot/grub/menu.lst" "WRONLY CREAT TRUNC"]
		puts $fh "timeout 0"
		puts $fh "default 0"
		puts $fh "\ntitle Genode on L4ka::Pistachio"
		puts $fh " kernel /pistachio/kickstart"
		puts $fh " module /pistachio/kernel"
		puts $fh " module /pistachio/sigma0"
		puts $fh " module /image.elf"
		close $fh
	}

	#
	# Build image
	#
	run_image

	if {[have_include "load/tftp"]} {
		#
		# Install PXE bootloader pulsar
		#
		install_pxe_bootloader_to_run_dir

		#
		# Generate pulsar config file
		#
		set fh [open "[run_dir]/config-52-54-00-12-34-56" "WRONLY CREAT TRUNC"]
		puts $fh " exec /boot/bender"
		puts $fh " load /pistachio/kickstart"
		puts $fh " load /pistachio/kernel"
		puts $fh " load /pistachio/sigma0"
		puts $fh " load /image.elf"
		close $fh

		generate_tftp_config
	}
}
