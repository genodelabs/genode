proc binary_name_ld_lib_so { } { return "ld-pistachio.lib.so" }
proc binary_name_core_a    { } { return "core-pistachio.a"    }
proc binary_name_timer     { } { return "pit_timer"           }

proc kernel_files { } { return { pistachio sigma0-pistachio kickstart-pistachio } }

proc run_boot_string   { } { return "\n\r\033\\\[1m\033\\\[33mL4Ka::Pistachio -" }
proc core_link_address { } { return "0x02000000" }

proc boot_output { } { return "serial" }

##
# Populdate boot directory with binaries on pistachio
#
proc run_boot_dir {binaries} {

	#
	# Build kernel and dynamic linker on demand, if not yet present in
	# '[run_dir]/genode/'
	#
	set kernel_arg ""
	set ld_arg     ""
	if {![file exists [run_dir]/genode/pistachio]} { set kernel_arg "kernel/pistachio" }
	if {![file exists [run_dir]/genode/ld.lib.so]} { set ld_arg     "lib/ld-pistachio" }
	set targets "$kernel_arg $ld_arg"

	if {[llength $targets]} { build $targets }

	if {$kernel_arg != ""} {
		copy_file bin/pistachio           [run_dir]/genode/pistachio
		copy_file bin/sigma0-pistachio    [run_dir]/genode/sigma0-pistachio
		copy_file bin/kickstart-pistachio [run_dir]/genode/kickstart-pistachio
	}

	build_core_image $binaries

	#
	# Move kernel files to distinct location within the boot directory so that
	# we can remove [run_dir]/genode after the core image has been built.
	#
	exec mkdir -p [run_dir]/boot
	copy_file [run_dir]/genode/pistachio           [run_dir]/boot/kernel
	copy_file [run_dir]/genode/sigma0-pistachio    [run_dir]/boot/sigma0
	copy_file [run_dir]/genode/kickstart-pistachio [run_dir]/boot/kickstart
	remove_genode_dir

	exec mv [run_dir]/image.elf [run_dir]/boot/image.elf

	if {[have_include "image/iso"] || [have_include "image/disk"]} {

		if {[have_include "image/disk"]} {
			install_disk_bootloader_to_run_dir
		}

		if {[have_include "image/iso"]} {
			install_iso_bootloader_to_run_dir
		}

		#
		# Generate GRUB2 config file
		#
		set fh [create_header_grub2_config]

		# The core binary is part of the 'binaries' list but it must
		# appear right after 'sigma0' as boot module. Hence the special case.
		#
		puts $fh "menuentry 'Genode on L4ka::Pistachio' {"
		puts $fh " insmod multiboot"
		puts $fh " multiboot /boot/bender [boot_output]"
		puts $fh " module /boot/kickstart"
		puts $fh " module /boot/kernel"
		puts $fh " module /boot/sigma0"
		puts $fh " module /boot/image.elf"
		puts $fh "}"
		close $fh
	}

	#
	# Build image
	#
	run_image

	if {[have_include "load/tftp"]} {
		#
		# Install PXE bootloader pulsar
		#
		install_pxe_bootloader_to_run_dir

		#
		# Generate pulsar config file
		#
		set fh [open "[run_dir]/config-52-54-00-12-34-56" "WRONLY CREAT TRUNC"]
		puts $fh " exec /boot/bender [boot_output]"
		puts $fh " load /boot/kickstart"
		puts $fh " load /boot/kernel"
		puts $fh " load /boot/sigma0"
		puts $fh " load /boot/image.elf"
		close $fh

		generate_tftp_config
	}
}


##
# Base source archive within depot
#
proc base_src { } {

	if {[have_spec x86_32]} { return base-pistachio }

	global specs

	puts stderr "Test requires base-pistachio kernel archive, which is missing for this build configuration"
	puts stderr "  SPECS=\"$specs\""
	exit 0
}
