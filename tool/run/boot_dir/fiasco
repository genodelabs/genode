proc binary_name_ld_lib_so { } { return "ld-fiasco.lib.so" }
proc binary_name_core_o    { } { return "fiasco/core.o" }
proc binary_name_timer     { } { return "fiasco_timer_drv" }


proc fiasco_serial_esc_arg { } { return "-serial_esc " }
proc run_boot_string       { } { return "\nL4 Bootstrapper" }
proc core_link_address     { } { return "0x01000000" }


##
# Populate boot directory with binaries on fiasco
#
proc run_boot_dir {binaries} {

	build { lib/ld/fiasco kernel/fiasco }

	build_core_image $binaries

	exec mkdir -p [run_dir]/fiasco
	exec cp kernel/fiasco/kernel    [run_dir]/fiasco
	exec cp kernel/fiasco/sigma0    [run_dir]/fiasco
	exec cp kernel/fiasco/bootstrap [run_dir]/fiasco

	if {[have_include "image/iso"] || [have_include "image/disk"]} {

		#
		# Install isolinux/GRUB files and bender
		#
		install_iso_bootloader_to_run_dir

		#
		# Generate GRUB config file
		#
		# The core binary is part of the 'binaries' list but it must
		# appear right after 'sigma0' as boot module. Hence the special case.
		#
		set fh [open "[run_dir]/boot/grub/menu.lst" "WRONLY CREAT TRUNC"]
		puts $fh "timeout 0"
		puts $fh "default 0"
		puts $fh "\ntitle Genode on L4/Fiasco"
		puts $fh " kernel /boot/bender"
		puts $fh " module /fiasco/bootstrap -serial"
		puts $fh " module /fiasco/kernel -serial -jdb_cmd=JH [fiasco_serial_esc_arg]"
		puts $fh " module /fiasco/sigma0"
		puts $fh " module /image.elf"
		puts $fh " vbeset 0x117 506070"
		close $fh
	}

	run_image

	if {[have_include "load/tftp"]} {
		#
		# Install PXE bootloader pulsar
		#
		install_pxe_bootloader_to_run_dir

		#
		# Generate pulsar config file
		#
		set fh [open "[run_dir]/config-52-54-00-12-34-56" "WRONLY CREAT TRUNC"]
		puts $fh " exec /boot/bender"
		puts $fh " load /fiasco/bootstrap -serial"
		puts $fh " load /fiasco/kernel -serial -serial_esc -jdb_cmd=JH"
		puts $fh " load /fiasco/sigma0"
		puts $fh " load /image.elf"
		close $fh

		generate_tftp_config
	}

	if {[have_include "load/ipxe"]} {
		create_ipxe_iso_config
		update_ipxe_boot_dir
		create_symlink_for_iso
	}
}
