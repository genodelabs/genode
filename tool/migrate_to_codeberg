#!/usr/bin/env expect
#
# \brief  Helper for updating git remote URLs
# \author Johannes Schlatow
# \date   2026-02-02
#

proc git { args } {
	return [exec -ignorestderr git {*}$args]
}

set paths [lmap p $argv { file normalize $p }]
foreach path $paths {
	if {![file isdirectory $path]} {
		continue }

	cd $path

	try {
		set result [git remote -v | grep -e github\.com\[:/\]genodelabs/genode-]
		set remotes [lsort -unique [lmap line [split $result \n] { lindex $line 0 }]]
	} trap CHILDSTATUS { } { continue
	} on error { msg }     { error $msg $::errorInfo }

	puts "\nIn $path,\nsome git remotes refer to repositories that"
	puts "migrated from GitHub to Codeberg. Furthermore, the 'master'"
	puts "branch has been renamed to 'main'. Note that the"
	puts "repositories at GitHub will no longer receive updates."
	puts "To update the repository URL and branch name, please execute"
	puts "the following commands:\n"
	puts "  cd $path"
	puts "  git branch -m master main"
	puts "  git branch --unset-upstream main"

	foreach remote $remotes {
		set url       [git remote get-url $remote]
		set repo_name [lindex [split $url /] end]

		if {[regexp "^https" $url]} {
			puts "  git remote set-url $remote https://codeberg.org/genodelabs/$repo_name"
		} else {
			puts "  git remote set-url $remote ssh://git@codeberg.org/genodelabs/$repo_name"
		}
		puts "  git branch -rd $remote/master"
	}

	puts ""
}

