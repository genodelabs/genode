#
# For running this script on Linux in your private network there is some
# preparation required. Let's assume 'eth0' is your physical network device
# (run 'ifconfig' for that), 'shrimp' is your Linux user name, and
# 192.168.178.1 is the IP of a device in your local network you want to ping.
# You would do the following:
#
# 1) Establish a bridge between a TAP device and a physical network device in
#    your Linux:
#    ! sudo brctl addbr br0
#    ! sudo ip addr flush dev eth0
#    ! sudo brctl addif br0 eth0
#    ! sudo ip tuntap add tap0 mode tap user shrimp
#    ! sudo brctl addif br0 tap0
#    ! sudo ifconfig eth0 up
#    ! sudo ifconfig tap0 up
#    ! sudo ifconfig br0 up
#    ! brctl show
#    ! sudo dhclient -v br0
#
# 2) Now you can do testing with the script:
#    ! cd <GENODE_DIR>/build/x86_64
#    ! make run/ping KERNEL=linux BOARD=linux ON_LINUX_WITH_DST_IP=192.168.178.1
#
# 3) Clean up your Linux when done testing:
#    ! sudo brctl delif br0 tap0
#    ! sudo ip tuntap delete tap0 mode tap
#    ! sudo brctl delif br0 eth0
#    ! sudo ifconfig br0 down
#    ! sudo brctl delbr br0
#    ! sudo ifconfig eth0 up
#    ! sudo dhclient -v eth0
#

source ${genode_dir}/repos/os/run/assert_nic.inc

set on_linux_with_dst_ip ""
catch {set on_linux_with_dst_ip $::env(ON_LINUX_WITH_DST_IP)}
set on_linux [expr ![string equal $on_linux_with_dst_ip ""]]

assert {!([have_spec linux] && !$on_linux)} \
	"You must set 'on_linux_with_dst_ip' in the run script if you want to run on Linux."

assert {!(![have_spec linux] && $on_linux)} \
	"You must run on Linux if 'on_linux_with_dst_ip' in the run script is set."

set on_hardware [expr ![have_include power_on/qemu]]
set second_ping_via_udp [expr $on_hardware && !$on_linux]

create_boot_directory

import_from_depot [depot_user]/src/[base_src] \
                  [depot_user]/pkg/[drivers_nic_pkg] \
                  [depot_user]/src/init \
                  [depot_user]/src/report_rom \
                  [depot_user]/src/nic_router

build { app/ping }

proc dst_ip { } {

	global on_linux_with_dst_ip
	global on_linux
	if {$on_linux} {
		return $on_linux_with_dst_ip
	}

	global on_hardware
	if {$on_hardware} {
		return "10.0.0.2"
	} else {
		return "10.0.2.2"
	}
}

append ping_1_start_node {
+ start ping_1 | ram: 8M
  + binary ping
  + config
  |        period_sec: 1
  |        verbose:    no
  |        count:      3
  |        dst_ip:     } [dst_ip] {
  |        report:     yes
  + route
    + service Report | + child report_rom
    + service Nic    | + child nic_router
    + any-service
      + parent
      + any-child}

append ping_2_start_node {
+ start ping_2 | ram: 8M
  + binary ping
  + config
  |        period_sec: 1
  |        verbose:    no
  |        count:      1
  |        protocol:   udp
  |        dst_ip:     } [dst_ip] {
  |        dst_port:   12345
  + route
    + service Nic | + child nic_router
    + any-service
      + parent
      + any-child}

if {!$second_ping_via_udp} { set ping_2_start_node "" }

install_config {
config
+ parent-provides
  + service ROM
  + service IRQ
  + service IO_MEM
  + service IO_PORT
  + service PD
  + service RM
  + service CPU
  + service LOG

+ default-route
  + any-service
    + parent
    + any-child

+ default | caps: 100 | ram: 1M

+ start timer
  + provides | + service Timer

} $ping_1_start_node $ping_2_start_node {

+ start drivers | caps: 1200 | ram: 32M | managing_system: yes
  + binary init
  + route
    + service ROM | label: config | + parent | label: drivers.config
    + service Uplink              | + child nic_router
    + any-service
      + parent
      + any-child

+ start report_rom | caps: 200
  + provides
    + service ROM
    + service Report
  + config | verbose: yes

+ start nic_router | caps: 200 | ram: 10M
  + provides
    + service Nic
    + service Uplink
  + config | dhcp_discover_timeout_sec: 1
    + policy | label_prefix: drivers | domain: uplink
    + policy | label_prefix: ping    | domain: downlink
    + domain uplink
      + nat | domain: downlink | icmp-ids: 100 | udp-ports: 100
    + domain downlink | interface: 10.0.3.1/24
      + dhcp-server | ip_first: 10.0.3.100 | ip_last: 10.0.3.200
      + icmp | dst: } [dst_ip] {/24 | domain: uplink
      + udp  | dst: } [dst_ip] {/24
        + permit | port: 12345 | domain: uplink
-
}

build_boot_image [build_artifacts]

append qemu_args " -nographic "
append_qemu_nic_args

set done_string ".*\"ping_1\" exited with exit value 0.*\n"
append_if $second_ping_via_udp done_string ".*ping_2\] From [dst_ip] Destination Unreachable.*\n"

proc test_timeout { } {
	if {[have_spec sel4] && [have_board pc]} { return 60 }
	return 30
}

run_genode_until $done_string [test_timeout]
