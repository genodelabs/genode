set use_usb_driver   [expr [have_spec omap4] || [have_spec platform_arndale] || [have_spec platform_rpi]]
set use_nic_driver   [expr !$use_usb_driver && ![have_spec platform_imx53] && ![have_spec linux] && ![have_spec hw_odroid_xu]]
set use_platform_drv [expr [have_spec platform_arndale] || [have_spec platform_imx53] || [have_spec platform_rpi]]

if {[expr !$use_usb_driver && !$use_nic_driver]} {
	puts "\n Run script is not supported on this platform. \n"; exit 0 }

set build_components {
	core init
	drivers/timer drivers/pci
	server/tcp_terminal
	server/nic_bridge
	noux/net lib/libc_noux
	noux-pkg/netcat
}

lappend_if $use_usb_driver                build_components drivers/usb
lappend_if $use_nic_driver                build_components drivers/nic
lappend_if [have_spec acpi]               build_components drivers/acpi
lappend_if [have_spec pci]                build_components drivers/pci/device_pd
lappend_if [have_spec gpio]               build_components drivers/gpio
lappend_if [have_include "power_on/qemu"] build_components drivers/uart
lappend_if $use_platform_drv              build_components drivers/platform

build $build_components

exec tar cfv bin/noux_netcat.tar -h -C bin/netcat .

#
# The '<build-dir>/bin/etc/' directory is expected to contain the
# files 'services', 'protocols', 'hosts', and 'resolv.conf'.
# Download these files from the FreeBSD source tree is possible.
#
exec mkdir -p bin/etc
set freebsd_url "http://svn.freebsd.org/base/release/8.2.0/etc"
foreach etc_file { services protocols hosts } {
	if {![file exists bin/etc/$etc_file]} {
		catch { exec wget -c -P bin/etc $freebsd_url/$etc_file } } }
exec touch bin/etc/resolv.conf

exec tar rfv bin/noux_netcat.tar -h -C bin/ etc

create_boot_directory

append config {
<config verbose="yes">
	<parent-provides>
		<service name="ROM"/>
		<service name="LOG"/>
		<service name="CAP"/>
		<service name="RAM"/>
		<service name="RM"/>
		<service name="CPU"/>
		<service name="PD"/>
		<service name="IRQ"/>
		<service name="IO_MEM"/>
		<service name="IO_PORT"/>
		<service name="SIGNAL"/>
	</parent-provides>
	<default-route>
		<any-service> <any-child/> <parent/> </any-service>
	</default-route>
	<start name="timer">
		<resource name="RAM" quantum="1M"/>
		<provides><service name="Timer"/></provides>
	</start>}

append_if [have_include "power_on/qemu"] config {
	<start name="uart_drv">
		<resource name="RAM" quantum="1M"/>
		<provides><service name="Terminal"/></provides>
		<config>
			<policy label="noux" uart="1"/>
		</config>
	</start>}

if {![have_include "power_on/qemu"]} {
append config {
	<start name="tcp_terminal">
		<resource name="RAM" quantum="5M"/>
		<provides> <service name="Terminal"/> </provides>
		<route>
			<service name="Nic"> <child name="nic_bridge"/> </service>
			<any-service> <parent/> <any-child/> </any-service>
		</route>
		<config>
			<policy label="noux_net" port="8888"/>
		</config>
	</start>
	<start name="nic_bridge">
		<resource name="RAM" quantum="4M"/>
		<provides><service name="Nic"/></provides>
		<route>}
append_if $use_nic_driver config {
			<service name="Nic"> <child name="nic_drv"/></service>}
append_if $use_usb_driver config {
			<service name="Nic"> <child name="usb_drv"/></service>}
append config {
			<any-service> <parent/> </any-service>
		</route>
	</start>}
}

append_if $use_nic_driver config {
	<start name="nic_drv">
		<resource name="RAM" quantum="4M"/>
		<provides> <service name="Nic"/> </provides>
	</start>}

append_if $use_platform_drv config {
	<start name="platform_drv">
		<resource name="RAM" quantum="1M"/>
		<provides>
			<service name="Platform"/>
			<service name="Regulator"/>
		</provides>
	</start>}

append_if [have_spec gpio] config {
	<start name="gpio_drv">
		<resource name="RAM" quantum="4M"/>
		<provides><service name="Gpio"/></provides>
		<config/>
	</start>}

append_if $use_usb_driver config {
	<start name="usb_drv">
		<resource name="RAM" quantum="12M"/>
		<provides>
			<service name="Nic"/>
			<service name="Input"/>
		</provides>
		<config uhci="no" ehci="yes" xhci="no">
			<nic mac="02:00:00:00:01:01"/>
		</config>
	</start>}

append_if [have_spec acpi] config {
	<start name="acpi">
		<resource name="RAM" quantum="6M" constrain_phys="yes"/>
		<binary name="acpi_drv"/>
		<provides>
			<service name="PCI"/>
			<service name="IRQ" />
		</provides>
		<config>
			<policy label="acpi_drv">
				<pci class="ALL"/>
			</policy>
			<policy label="nic_drv">
				<pci class="ETHERNET"/>
			</policy>
		</config>
		<route>
			<any-service> <parent/> </any-service>
		</route>
	</start>}

append_if [expr ![have_spec acpi] && [have_spec pci]] config {
	<start name="pci_drv">
		<resource name="RAM" quantum="2M" constrain_phys="yes"/>
		<provides> <service name="PCI"/> </provides>
		<config>
			<policy label="nic_drv">
				<pci class="ETHERNET"/>
			</policy>
		</config>
	</start> }
	
append config {
	<start name="noux_net">
		<resource name="RAM" quantum="1G"/>
		<config>
			<fstab> <tar name="noux_netcat.tar" at="/"/> </fstab>
			<start name="/bin/netcat">
				<arg value="-l"/>
				<arg value="-p 5555"/>
			</start>
		</config>}

append_if [expr ![have_include "power_on/qemu"]] config {
		<route>
			<service name="Nic"> <child name="nic_bridge"/> </service>
			<any-service> <parent/> <any-child/> </any-service>
		</route>}

append config {
	</start>
</config>
}

install_config $config

#
# Boot modules
#

# generic modules
set boot_modules {
	core init timer tcp_terminal nic_bridge
	ld.lib.so noux_net libc.lib.so libm.lib.so libc_lock_pipe.lib.so
	libc_resolv.lib.so libc_noux.lib.so lwip.lib.so noux_netcat.tar
}

# platform-specific modules
lappend_if $use_nic_driver                boot_modules nic_drv
lappend_if $use_usb_driver                boot_modules usb_drv
lappend_if [have_spec pci]                boot_modules pci_drv
lappend_if [have_spec acpi]               boot_modules acpi_drv
lappend_if [have_spec nova]               boot_modules pci_device_pd
lappend_if [have_include "power_on/qemu"] boot_modules uart_drv
lappend_if $use_platform_drv              boot_modules platform_drv
lappend_if [have_spec gpio]               boot_modules gpio_drv

build_boot_image $boot_modules

set noux_output_file "noux_output.log"
set serial_ip_addr   "localhost"
set noux_ip_addr     "localhost"

if {[have_include "power_on/qemu"]} {
	#
	# Redirect the output of Noux via the virtual serial port 1 into a file to be
	# dumped after the successful completion of the test.
	append qemu_args " -nographic"
	append qemu_args " -serial mon:stdio"
	append qemu_args " -serial file:$noux_output_file"

	append_if [have_spec x86]     qemu_args " -net nic,model=e1000 "
	append_if [have_spec lan9118] qemu_args " -net nic,model=lan9118 "

	append qemu_args " -net user -redir tcp:5555::5555 "

	run_genode_until {.*got IP address.*\n} 60
	set serial_id [output_spawn_id]
} else {
	#
	# We have to look for 'got IP address' twice because tcp_terminal
	# as well as noux_net will request one and we do not know the
	# order in advance.
	run_genode_until {.*got IP address.*} 60
	set serial_id [output_spawn_id]
	run_genode_until {.*got IP address.*\n} 10 $serial_id

	regexp {\[init -> noux_net\] .{1,5}got IP address [0-9]+\.[0-9]+\.[0-9]+\.[0-9]+} $output noux_ip_addr
	regexp {[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+} $noux_ip_addr noux_ip_addr

	regexp {\[init -> tcp_terminal\] .{1,5}got IP address [0-9]+\.[0-9]+\.[0-9]+\.[0-9]+} $output serial_ip_addr
	regexp {[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+} $serial_ip_addr serial_ip_addr

	# connect to Genode target and reading log output via network connection

	spawn socat -u tcp:$serial_ip_addr:8888 OPEN:$noux_output_file,creat,append
}

sleep 4

puts "--- serial '$serial_ip_addr' noux '$noux_ip_addr'"
exec echo -e "Hello Genode" | netcat $noux_ip_addr 5555 &

run_genode_until {exited with exit value 1} 20 $serial_id

sleep 4

set output [exec cat $noux_output_file]
puts "\noutput:\n$output\n"
exec rm $noux_output_file

exec rm bin/noux_netcat.tar
exec rm -r bin/etc

if {![regexp {Hello Genode} $output dummy]} {
	puts stderr "Error: output not as expected"
	exit 1
}
